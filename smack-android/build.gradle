apply plugin: 'android-library'
apply plugin: 'android-maven'
apply plugin: 'signing'

description = """\
Smack for Android"""

repositories {
      mavenCentral()
      mavenLocal()
}

dependencies {
	compile 'dnsjava:dnsjava:2.1.6'
	compile project(':smack-core')
	compile project(':smack-tcp')
	compile project(':smack-extensions')
	compile project(':smack-experimental')
	compile project(':smack-resolver-dnsjava')
}

android {
	compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
    	versionCode 1
    	versionName "##smackVersion##"
        minSdkVersion 7
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  if (name.equals(com.android.builder.BuilderConstants.DEBUG)) {
    return; // Skip debug builds.
  }
  task("javadoc${variant.name.capitalize()}", type: Javadoc) {
    description "Generates Javadoc for $variant.name."
    source = variant.javaCompile.source
    options.encoding = "UTF-8"
    ext.androidJar = "${android.plugin.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
  }

  task("bundleJavadoc${variant.name.capitalize()}", type: Jar) {
    description "Bundles Javadoc into zip for $variant.name."
    classifier = "javadoc"
    from tasks["javadoc${variant.name.capitalize()}"]
  }

}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java
}

task jar(type: Jar) {
  from 'build/classes/release'
}

task javadocJar(type: Jar, dependsOn: "javadocRelease") {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

artifacts {
  archives sourcesJar
  archives javadocJar
  archives jar
}